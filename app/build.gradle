plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp' version '1.9.23-1.0.19'
}

android {
    compileSdk 34

    defaultConfig {
        namespace 'com.gswxxn.camerasnap'
        applicationId namespace
        minSdk 28
        targetSdk 34
        versionCode 1700
        versionName "1.7"
        //noinspection ChromeOsAbiSupport
        ndk.abiFilters += "arm64-v8a"
        //noinspection ChromeOsAbiSupport
        ndk.abiFilters += "armeabi-v7a"
        applicationVariants.configureEach { variant ->
            variant.outputs.configureEach {
                outputFileName = "UnlockMIUICameraSnap_" + versionName + "(" + versionCode + ").apk"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        buildConfig = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    packagingOptions {
        resources {
            excludes += "/META-INF/**"
            excludes += "/kotlin/**"
            excludes += "/*.txt"
            excludes += "/*.bin"
            excludes += "/*.json"
        }
        dex {
            useLegacyPackaging = true
        }
    }
    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs = [
                '-Xno-param-assertions',
                '-Xno-call-assertions',
                '-Xno-receiver-assertions'
        ]
    }
}

dependencies {
    compileOnly 'de.robv.android.xposed:api:82'
    implementation 'com.highcapable.yukihookapi:api:1.2.0'
    ksp 'com.highcapable.yukihookapi:ksp-xposed:1.2.0'
    implementation 'org.luckypray:DexKit:1.1.8'
    implementation 'androidx.annotation:annotation:1.7.1'
    compileOnly project(":api:api")
}